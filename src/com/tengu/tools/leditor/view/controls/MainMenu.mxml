<?xml version="1.0" encoding="utf-8"?>
<mx:MenuBar xmlns:fx="http://ns.adobe.com/mxml/2009" 
			xmlns:s="library://ns.adobe.com/flex/spark" 
			xmlns:mx="library://ns.adobe.com/flex/mx"
			labelField="@label"
			dataProvider="{applicationMenu}"
			itemClick="onClickItem(event)"
			menuShow="onShowMenu(event)"
			menuHide="onHideMenu(event)"
			>
	<fx:Declarations>
		<fx:XMLList id="applicationMenu">
			<menuitem label="File">
				<menuitem label="New" data="{NEW_PROJECT}" />
				<menuitem label="Load" data="{LOAD_PROJECT}"/>
				<menuitem label="Save" data="{SAVE_PROJECT}" enabled="{!model.files.saved}"/>
				<menuitem label="Save as" data="{SAVE_PROJECT_AS}" enabled="{model.layers.layerList.length > 0}"/>
				<menuitem type="separator"/>
				<menuitem label="Quit" data="{QUIT}" enabled="{model.files.saved}"/>
			</menuitem>
			<menuitem label="View">
				<menuitem label="Center camera"  data="{MOVE_CAMERA_TO_CENTER}"/>
			</menuitem>
			<menuitem type="separator"/>
			<menuitem label="Help">
				<menuitem label="About" data="About" />
			</menuitem >
		</fx:XMLList>
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			import com.tengu.tools.leditor.logic.api.IEditorController;
			import com.tengu.tools.leditor.model.LedModel;
			import com.tengu.tools.leditor.view.controls.windows.ConfirmWindow;
			
			import mx.events.CloseEvent;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;

			private static const NEW_PROJECT:String 	= "new";
			private static const LOAD_PROJECT:String 	= "load";
			private static const SAVE_PROJECT:String 	= "save";
			private static const SAVE_PROJECT_AS:String = "save_as";
			private static const QUIT:String 			= "quit";
			
			private static const CONFIRM_CLOSE:String 	= "confirmClose";
			private static const CONFIRM_CLEAR:String 	= "confirmClear";
			private static const CONFIRM_LOAD:String 	= "confirmLoad";

			private static const MOVE_CAMERA_TO_CENTER:String 	= "move-to-center";
			
			private var window:ConfirmWindow;
			private var confirmType:String;

			[Inject]
			public var controller:IEditorController;
			
			[Inject]
			[Bindable]
			public var model:LedModel;

			[Inject]
			public var mainApp:Led;
			
			public function showClosingAlert():void
			{
				showConfirmWindow(CONFIRM_CLOSE);
			}
			
			private function showConfirmWindow(type:String):void
			{
				if (window != null)
				{
					return;
				}
				confirmType = type;
				
				window = PopUpManager.createPopUp(mainApp, ConfirmWindow, true) as ConfirmWindow;
				window.message = "Сохранить изменения?";
				window.addEventListener(CloseEvent.CLOSE, onCloseConfirm);
				PopUpManager.centerPopUp(window);
			}
			
			private function onClickItem(event:MenuEvent):void
			{
				var menuItem:String = event.item.@data; 
				
				switch (menuItem) 
				{
					case NEW_PROJECT:
						if (!model.files.saved)
						{
							showConfirmWindow(CONFIRM_CLEAR);
						}
						else
						{
							controller.clearAll();
						}				
						break;
					case LOAD_PROJECT:
						if (!model.files.saved)
						{
							showConfirmWindow(CONFIRM_LOAD);
						}
						else
						{
							controller.loadProject();
						}				
						break;
					case SAVE_PROJECT:
						controller.saveProject();
						break;
					case SAVE_PROJECT_AS:
						controller.saveProject(true);
						break;
					case QUIT:
						if (!model.files.saved)
						{
							showClosingAlert();
						}
						else
						{
							mainApp.close();
						}
						break;
					case MOVE_CAMERA_TO_CENTER:
						controller.moveCameraToCenter();
						break;

				}
			}
			
			protected function onShowMenu(event:MenuEvent):void
			{
				model.screenSettings.locked = true;
			}
			
			protected function onHideMenu(event:MenuEvent):void
			{
				model.screenSettings.locked = false;
			}
			
			private function onCloseConfirm(event:CloseEvent):void
			{
				if (window.result)
				{
					controller.saveProject();
				}
				window.removeEventListener(CloseEvent.CLOSE, onCloseConfirm);
				window = null;
				model.files.saved = true;
				switch (confirmType)
				{
					case CONFIRM_CLOSE:
						mainApp.close();
						break;
					case CONFIRM_CLEAR:
						controller.clearAll();
						break;
					case CONFIRM_LOAD:
						controller.loadProject();
						break;
				}
			}
			
		]]>
	</fx:Script>
</mx:MenuBar>
